plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.10' apply false
    id 'io.spring.dependency-management' version '1.1.6' apply false
    id 'com.palantir.docker' version '0.35.0' apply false
    id 'jacoco' // 代码覆盖率
    id 'checkstyle' // 代码风格检查
    id 'pmd' // 代码质量检查
    id 'com.github.spotbugs' version '6.0.25' apply false // 静态代码分析
}

group = 'com.evcs'
version = '1.0.0'

repositories {
    maven { url 'https://maven.aliyun.com/repository/public' }
    maven { url 'https://maven.aliyun.com/repository/central' }
    maven { url 'https://repo1.maven.org/maven2' }
    mavenCentral()
}

// Java版本配置
allprojects {
    apply plugin: 'java'
    
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
    
    repositories {
        maven { url 'https://maven.aliyun.com/repository/public' }
        maven { url 'https://maven.aliyun.com/repository/central' }
        maven { url 'https://repo1.maven.org/maven2' }
        mavenCentral()
    }
}

// 子项目通用配置
subprojects {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: 'com.github.spotbugs'

    group = rootProject.group
    version = rootProject.version
    
    // 依赖版本管理
    ext {
        springCloudVersion = '2023.0.3'  // 回退：解决ForwardedHeadersFilter兼容性问题
        postgresqlVersion = '42.7.4'  // 升级：安全补丁和性能优化
        redisVersion = '5.0.1'
        jwtVersion = '3.19.2'  // 稳定版本 - 改为3.19.2解决Maven仓库找不到4.4.0的问题
        mybatisPlusVersion = '3.5.8'  // 安全修复版本
    mybatisSpringVersion = '3.0.3'  // Spring Boot 3.2.x兼容版本
        fastjson2Version = '2.0.51'  // 性能优化版本
        hutoolVersion = '5.8.32'  // 最新稳定版本
        knife4jVersion = '4.5.0'
        logstashLogbackVersion = '7.4'
        resilience4jVersion = '2.2.1'  // 性能优化版本
    }
    
    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
        dependencies {
            dependency "org.mybatis:mybatis-spring:${mybatisSpringVersion}"
        }
    }
    
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        
        // Lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
    }
    
    tasks.named('test') {
        useJUnitPlatform()
        
        // 测试失败后继续执行其他测试
        ignoreFailures = false
        
        // 显示测试输出
        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat "full"
            showStandardStreams = false
        }
        
        // 生成测试报告
        finalizedBy jacocoTestReport
    }
    
    // JaCoCo测试覆盖率报告配置
    jacocoTestReport {
        dependsOn test
        
        reports {
            xml.required = true
            html.required = true
            html.outputLocation = file("${buildDir}/reports/jacoco")
        }
        
        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                    '**/entity/**',
                    '**/dto/**',
                    '**/vo/**',
                    '**/*Application.class'
                ])
            }))
        }
    }
    
    // JaCoCo覆盖率验证
    jacocoTestCoverageVerification {
        violationRules {
            rule {
                enabled = false // 暂时禁用，项目成熟后可启用
                limit {
                    minimum = 0.60 // 60%覆盖率
                }
            }
        }
    }
    
    // 编译配置
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
    
    def hasBootApplication = fileTree('src/main/java') {
        include '**/*Application.java'
    }.files
    
    tasks.named('bootJar') {
        enabled = !hasBootApplication.isEmpty()
    }
    tasks.named('jar') {
        enabled = hasBootApplication.isEmpty()
    }
}

// 代码质量检查配置
configure(subprojects.findAll { it.name.startsWith('evcs-') }) {

    // Checkstyle配置
    checkstyle {
        toolVersion = '10.17.0'
        configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
        ignoreFailures = false
        maxWarnings = 0
        showViolations = true
    }

    // PMD配置
    pmd {
        toolVersion = '7.0.0'
        ruleSetFiles = files("${rootDir}/config/pmd/ruleset.xml")
        ignoreFailures = false
        consoleOutput = true
    }

    // SpotBugs配置
    spotbugs {
        toolVersion = '4.8.6'
        ignoreFailures = false
        showStackTraces = true
        showProgress = true
    }

    // Jacoco配置 - 代码覆盖率
    jacoco {
        toolVersion = '0.8.12'
        reportsDirectory = file("${buildDir}/reports/jacoco")
    }

    jacocoTestReport {
        reports {
            xml.required.set(true)
            html.required.set(true)
            csv.required.set(false)
        }
    }

    // 测试任务配置
    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat "full"
        }
    }

    // 代码质量检查任务
    task('qualityCheck') {
        dependsOn checkstyleMain, pmdMain, spotbugsMain, test
        description = 'Run all code quality checks'
    }
}

// Docker构建配置
configure(subprojects.findAll { !it.name.endsWith('-common') }) {
    apply plugin: 'com.palantir.docker'

    def registryHost = 'registry.cn-hangzhou.aliyuncs.com'

    docker {
        name "${registryHost}/${project.group}/${project.name}:${project.version}"
        dockerfile file('Dockerfile')
        files bootJar.archiveFile
        buildArgs(['JAR_FILE': "${bootJar.archiveFileName}"])
        tag 'latest', "${registryHost}/${project.group}/${project.name}:latest"
    }
}